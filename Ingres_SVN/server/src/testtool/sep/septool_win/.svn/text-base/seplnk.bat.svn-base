@echo off
REM seplnk : Link EQUEL/ESQL object file with various INGRES libraries
REM
REM History:
REM	17-oct-1995 (somsa01)
REM		Created from seplnk.sh .
REM	03-Jan-1996 (somsa01)
REM		Added /nologo and -DNT_GENERIC to cl options.
REM
REM 	03-Apr-1997 (clate01)
REM		Shortened label names, in order to run successfully
REM		on Windows'95, label names longer than 8 chars are
REM		truncated, which caused problems, also added iiapi.lib
REM		to the linked libraries list.
REM	13-Nov-1998 (somsa01)
REM		Changed "iiapi.lib" to "oiapi.lib".
REM	30-sep-2001 (somsa01)                                         
REM		Removed restriction of when to add %II_SYSTEM%\ingres\utility   
REM		to the PATH.
REM	19-dec-2001 (somsa01)
REM		Use ingres64.lib on Win64.
REM	02-oct-2003 (somsa01)
REM		Ported to NT_AMD64.
REM	18-aug-2004 (drivi01)
REM		Updated link libraries to new jam libraries.
REM     11-oct-2004 (drivi01)
REM             Removed some unecessary code, which was causing SEGV's when
REM             the PATH was too long.
REM	21-oct-2004 (drivi01)
REM		Backing out change from 11-oct-2004, b/c it isn't needed.
REM	17-May-2005 (lakvi01)
REM		Link with bufferoverflowu.lib on WIN64 (i64_win and a64_win).
set CMD=seplnk
setlocal

if not "%1"=="" goto CONTINU
echo.
PCecho "Usage: seplnk <file1> [<file2> ... <filen>]"
echo.
echo.
PCecho "        -c[onditional]   Conditional Linking. Only link if"
PCecho "                         image file does not exist."
echo.
goto DONE
 
:CONTINU
REM++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
REM+
REM+ Check local Ingres environment
REM+
REM++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

if not "%II_SYSTEM%"=="" goto CONTINU2
echo.
PCecho "|------------------------------------------------------------|"
PCecho "|                      E  R  R  O  R                         |"
PCecho "|------------------------------------------------------------|"
PCecho "|     The local Ingres environment must be setup and the     |"
PCecho "|     the installation running before continuing             |"
PCecho "|------------------------------------------------------------|"
echo.
goto DONE

:CONTINU2
call ipset tmpdir ingprenv II_TEMPORARY
echo %PATH%> %tmpdir%\seplnk.tmp
call ipset tmpvar sed -e "s/;/ /g" %tmpdir%\seplnk.tmp
del %tmpdir%\seplnk.tmp
set path=%II_SYSTEM%\ingres\utility;%path%

REM++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

if "%CC%"=="" set CC=CL
set imageflg=true

REM conditional
set cflag=false

set line_args=%1
:LOOP
shift
if "%1"=="" goto ENDLOOP
set line_args=%line_args% %1
goto LOOP

:ENDLOOP
for %%i in (%line_args%) do echo %%i>> %tmpdir%\seplnk.tmp
call ipset count expr 0
:LOOP2
call ipset count expr %count% + 1
call ipset arg PCread %tmpdir%\seplnk.tmp %count%
if "%arg%"=="" goto ENDLOOP2
  if "%arg%"=="-conditional" set cflag=true
  if "%arg%"=="-conditiona" set cflag=true
  if "%arg%"=="-condition" set cflag=true
  if "%arg%"=="-conditio" set cflag=true
  if "%arg%"=="-conditi" set cflag=true
  if "%arg%"=="-condit" set cflag=true
  if "%arg%"=="-condi" set cflag=true
  if "%arg%"=="-cond" set cflag=true
  if "%arg%"=="-con" set cflag=true
  if "%arg%"=="-co" set cflag=true
  if "%arg%"=="-c" set cflag=true
  if "%cflag%"=="true" goto LOOP2

  echo %arg%| cut -c1 > %tmpdir%\seplnk2.tmp
  call ipset test_flag PCread %tmpdir%\seplnk2.tmp 1
  del %tmpdir%\seplnk2.tmp
  if "%test_flag%"=="-" echo bad flag %arg%
  goto LOOP2

:ENDLOOP2
set do_it=false
call ipset count expr 0
:LOOP3
call ipset count expr %count% + 1
call ipset arg PCread %tmpdir%\seplnk.tmp %count%
if "%arg%"=="" goto ENDLOOP3
  echo %arg%| cut -c1 > %tmpdir%\seplnk2.tmp
  call ipset test_flag PCread %tmpdir%\seplnk2.tmp 1
  del %tmpdir%\seplnk2.tmp
  if "%test_flag%"=="-" goto ENDCASE

  echo %arg% | cut -f 2 -d "." > %tmpdir%\seplnk2.tmp
  call ipset test_flag PCread %tmpdir%\seplnk2.tmp 1
  del %tmpdir%\seplnk2.tmp
  if not "%test_flag%"=="obj" goto NEXTCAS
    set cfilnam=%cfilnam% %arg%
    call ipset tfilnam basename %arg% .obj
    goto ENDCASE

:NEXTCAS
  echo %arg% | cut -f 2 -d "." > %tmpdir%\seplnk2.tmp
  call ipset test_flag PCread %tmpdir%\seplnk2.tmp 1
  del %tmpdir%\seplnk2.tmp
  if not "%test_flag%"=="lib" goto NEXTCAS2
    set cfilnam=%cfilnam% %arg%
    call ipset tfilnam basename %arg% .lib
    goto ENDCASE

:NEXTCAS2
  if not exist %arg%.lib goto NEXTIF
    set ofilnam=%arg%.lib
    set cfilnam=%cfilnam% %ofilnam%
    call ipset tfilnam basename %ofilnam% .lib
    goto ENDCASE
:NEXTIF
  if not exist %arg%.obj goto NEXTIF2
    set ofilnam=%arg%.obj
    set cfilnam=%cfilnam% %ofilnam%
    call ipset tfilnam basename %ofilnam% .obj
    goto ENDCASE
:NEXTIF2
  set cfilnam=%cfilnam% %arg%
  call ipset tfilnam basename %arg%

:ENDCASE
  if "%imageflg%"=="false" goto LOOP3
    set filnam=%tfilnam%
    set imageflg=false
    set do_it=true
    goto LOOP3

:ENDLOOP3
if "%do_it%"=="true" if "%cflag%"=="true" if exist %filnam%.exe set do_it=false

if not "%do_it%"=="true" goto DONE
  if "%CPU%"=="IA64" set libingfiles=%II_SYSTEM%\ingres\lib\ingres64.lib %II_SYSTEM%\ingres\lib\iilibapi64.lib& goto RUNCC
  if "%CPU%"=="AMD64" set libingfiles=%II_SYSTEM%\ingres\lib\ingres64.lib %II_SYSTEM%\ingres\lib\iilibapi64.lib& goto RUNCC
  set libingfiles=%II_SYSTEM%\ingres\lib\libingres.lib %II_SYSTEM%\ingres\lib\iilibapi.lib

:RUNCC
  if "%CPU%"=="IA64" set bufferlib=bufferoverflowu.lib
  if "%CPU%"=="AMD64" set bufferlib=bufferoverflowu.lib
  %CC% /nologo -DNT_GENERIC -Fe%filnam%.exe %cfilnam% %libingfiles% msvcrt.lib kernel32.lib user32.lib advapi32.lib %bufferlib%

:DONE
endlocal
